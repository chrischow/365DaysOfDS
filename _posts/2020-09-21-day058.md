---
layout: post
title: "Day 58: Excel VBA"
subtitle: "Automating Documentation"
date: 2020-09-21 22:00:00 +0800
background: '/img/posts/ml.jpg'
---

I took a break from the MTARSI project today to focus on something related to my actual work. I'm currently writing policy for data governance, and figured that policies could be more easily accessed if there was metadata on each policy.

## Restructuring the Document
Why structure the document? Imposing some structure on unstructured data allows us to (i) include metadata (see below), (ii) add version control, and (iii) programmatically format a document. In my workplace, I intend to establish it as a list in SharePoint, create quick-access views for various stakeholders, and have features to build the policy document into a Word document using Excel.

## Policy Metadata
To add metadata, I copied the policy statements into an Excel sheet with the following columns:

| Column | Purpose |
| :----- | :------ |
| S/N | For sorting policy clauses and aesthetic features like headers and figure/table titles. |
| Clause | The policy clause e.g. 1, 2, 3a, 3b, 4a, 4b-i, 4b-ii. |
| Heading | The heading level i.e. H1/H2/H3. Policy clauses have no heading.
| Title | Paragraph title. |
| Text | Header or paragraph text. |
| Category | Sub-section of policy document. |
| Section | Section of policy document. |
| Bold | (To be implemented) For text to be presented in bold. |
| Italics | (To be implemented) For text to be presented in italics. |
| Stakeholder(s) | (In separate columns) The applicable party in a given policy. |

Each row contained either (a) a header, (b) a paragraph, or (c) a figure/table title. 

## Rendering the Document
I wrote some draft VBA code (see end of post) on some placeholder text (because this is on my own computer) to build a Word document:

1. Define a custom outline numbering format based on the Civil Service style.
2. Loop through each row:
    1. For headings (`H1/H2/H3`): apply the appropriate heading style
    2. For figure/table titles (`TT`): apply the figure/table title style
    3. For policy statements:
        1. Input the paragraph title and text.
        2. Format the paragraph title.
        3. Apply the outline numbering format.
    4. Add line breaks.
    5. Reset the paragraph and font styles.

The document came out perfectly!

```vb
Sub BuildPolicyDocument()

Dim wordApp As Word.Application, wordDoc As Word.Document, ws As Worksheet
Dim currentPara As Word.Range
Dim formatPara As Word.Range
Dim titleWordCount As Integer

Set wordApp = New Word.Application
Set wordDoc = wordApp.Documents.Add
Set ws = ActiveWorkbook.Worksheets(1)
Set olt = wordApp.ListGalleries(wdOutlineNumberGallery).ListTemplates(1)

' Number formatting
With olt.ListLevels(1)
    .NumberFormat = "%1."
    .TabPosition = CentimetersToPoints(1.5)
    .NumberPosition = CentimetersToPoints(0)
    .TextPosition = CentimetersToPoints(0)
    .Alignment = wdListLevelAlignLeft
End With

With olt.ListLevels(2)
    .NumberFormat = "%2."
    .TabPosition = CentimetersToPoints(3#)
    .NumberPosition = CentimetersToPoints(1.5)
    .TextPosition = CentimetersToPoints(1.5)
    .Alignment = wdListLevelAlignLeft
    .NumberStyle = wdListNumberStyleLowercaseLetter
End With

With olt.ListLevels(3)
    .NumberFormat = "%3."
    .TabPosition = CentimetersToPoints(4.5)
    .NumberPosition = CentimetersToPoints(3)
    .TextPosition = CentimetersToPoints(3)
    .Alignment = wdListLevelAlignLeft
    .NumberStyle = wdListNumberStyleLowercaseRoman
End With

' Set formats
With wordDoc.Paragraphs(wordDoc.Paragraphs.Count).Range
    With .Font
        .Name = "Times New Roman"
        .Size = 14
    End With
    With .ParagraphFormat
        .Alignment = wdAlignParagraphJustify
        .LineSpacingRule = wdLineSpaceSingle
    End With
    
End With

' Find last row
lastRow = Cells(Rows.Count, 1).End(xlUp).Row

Dim i As Integer
For i = 2 To lastRow
    
    ' Define object
    Set currentPara = wordDoc.Paragraphs(wordDoc.Paragraphs.Count).Range
    currentPara.ParagraphFormat.SpaceAfter = 0
    currentPara.ParagraphFormat.SpaceBefore = 0
    
    ' H1
    If ws.Range("C" & i).Value = "H1" Then
        
        ' Insert header in bold and uppercase
        currentPara.Bold = True
        currentPara = UCase(ws.Range("F" & i).Value)
        
    ElseIf ws.Range("C" & i).Value = "H2" Then
        
        ' Insert header in bold
        currentPara.Bold = True
        currentPara = ws.Range("F" & i).Value
    
    ElseIf ws.Range("C" & i).Value = "TT" Then
    
        ' Insert header in bold
        currentPara.Underline = wdUnderlineSingle
        currentPara = ws.Range("F" & i).Value
        currentPara.Font.Size = 12
        currentPara.ParagraphFormat.Alignment = wdAlignParagraphCenter
    
    Else
        
        ' Input numbering and text
        currentPara.Bold = False
        If ws.Range("E" & i).Value <> "" Then
            titleWordCount = ws.Range("E" & i).Characters.Count
            currentPara = ws.Range("E" & i).Value & ". " & ws.Range("F" & i).Value
            
            For j = 1 To titleWordCount
                currentPara.Characters(j).Underline = wdUnderlineSingle
            Next j
                
        Else
            currentPara = ws.Range("F" & i).Value
        End If
        
        ' Apply numbering
        currentPara.ListFormat.ApplyListTemplateWithLevel _
            ListTemplate:=olt, ContinuePreviousList:=True, ApplyLevel:=ws.Range("D" & i).Value
    
    End If
    
    ' Add next line
    currentPara.InsertParagraphAfter
    wordDoc.Paragraphs(wordDoc.Paragraphs.Count).Range.ListFormat.RemoveNumbers
    wordDoc.Paragraphs(wordDoc.Paragraphs.Count).Range.InsertParagraphAfter
    With wordDoc.Paragraphs(wordDoc.Paragraphs.Count).Range
        .Underline = wdUnderlineNone
        .Font.Size = 14
        .ParagraphFormat.Alignment = wdAlignParagraphJustify
    End With
    
Next i


' Display word
wordApp.Visible = True

' Clear variables
Set wordDoc = Nothing
Set wordApp = Nothing

End Sub

```